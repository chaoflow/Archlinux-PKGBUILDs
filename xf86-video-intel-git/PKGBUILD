# Contributor: breakdown <breakdown[at]archlinux[dot]us>
# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: William Heinbockel <wheinbockel[at]gmail[dot]com>

pkgname=xf86-video-intel-git
pkgver=20091204
pkgrel=1
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
url="http://xorg.freedesktop.org/"
arch=('i686' 'x86_64')
license=('custom')
depends=('intel-dri>=7.5' 'libpciaccess>=0.10.6' 'libdrm>=2.4.16' 'libxvmc')
makedepends=('git' 'pkgconfig' 'xorg-server>=1.6.3' 'xf86driproto>=2.1.0' 'glproto>=1.4.10'
	     'mesa>=7.5' 'xineramaproto' 'xorg-util-macros>=1.3.0')
conflicts=('xorg-server<1.6.2' 'xf86-video-i810' 'xf86-video-intel' 'xf86-video-intel-legacy')
provides=('xf86-video-intel=2.9')
options=('!libtool')
source=('LICENSE')
md5sums=('b924480931774dec14515e270db4924d')

_gitroot="git://anongit.freedesktop.org/xorg/driver/xf86-video-intel"
_gitname="xf86-video-intel"

build() {
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
    cd $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  ./autogen.sh --prefix=/usr --enable-dri || return 1
  make || return 1
  make DESTDIR="$pkgdir" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/xf86-video-intel/LICENSE"
}
