# Maintainer: Florian Friesdorf <archlinux@chaoflow.net>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=rxvt-unicode-cvs
_pkgname=rxvt-unicode
pkgver=20091201
pkgrel=1
pkgdesc="An unicode enabled rxvt-clone terminal emulator (urxvt) from cvs"
arch=('i686' 'x86_64')
url="http://software.schmorp.de/pkg/rxvt-unicode.html"
license=("GPL2")
depends=('gcc-libs' 'libxft' 'libxpm')
makedepends=('cvs' 'ncurses' 'perl>=5.10.0' 'pkgconfig')
optdepends=('perl: lots of utilities' 'gtk2-perl: to use the urxvt-tabbed')
provides=('rxvt-unicode')
conflicts=('rxvt-unicode')
source=(${_pkgname}.desktop ${_pkgname}.png)
md5sums=('5bfefa1b41c2b81ca18f2ef847330543'
         '84328cada91751df07324d95f8be4d1b')

_cvsroot=":pserver:anonymous:@cvs.schmorp.de/schmorpforge"
_cvsmod="rxvt-unicode"

build() {
  cd ${srcdir}

  msg "Connecting to ${_cvsmod} CVS server...."
  if [ -d ${_cvsmod}/CVS ]; then
    cd ${_cvsmod}
    cvs -z3 update -d
  else
    cvs -z3 -d ${_cvsroot} co -D ${pkgver} -f ${_cvsmod}
    cd ${_cvsmod}
  fi

  msg "CVS checkout done or server timeout"
  msg "Starting make..."

  mkdir -p ${srcdir}/${_cvsmod}-build
  cd ${srcdir}/${_cvsmod}-build

  ../${_srcdir}/${_cvsmod}/configure --prefix=/usr \
    --with-terminfo=/usr/share/terminfo \
    --enable-xft \
    --enable-font-styles \
    --enable-perl \
    --enable-xim \
    --enable-keepscrolling \
    --enable-selectionscrolling \
    --enable-mousewheel \
    --enable-slipwheeling \
    --enable-smart-resize \
    --enable-xpm-background \
    --enable-transparency \
    --enable-pointer-blank \
    --enable-utmp \
    --enable-wtmp \
    --enable-lastlog
  make || return 1
  mkdir -p ${startdir}/pkg/usr/share/terminfo
  export TERMINFO=${startdir}/pkg/usr/share/terminfo
  make DESTDIR=${startdir}/pkg install
 # install the tabbing wrapper ( requires gtk2-perl! )
  cp $srcdir/$_cvsmod/doc/rxvt-tabbed $srcdir/${_cvsmod}-build/doc
  sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed
  install -Dm 755 doc/rxvt-tabbed ${startdir}/pkg/usr/bin/urxvt-tabbed
 # install freedesktop menu and icon ( icon from cvs checkout )
  install -Dm644 ../${_pkgname}.desktop \
    ${startdir}/pkg/usr/share/applications/${_pkgname}.desktop
  install -Dm644 ../${_pkgname}.png \
    ${startdir}/pkg/usr/share/pixmaps/${_pkgname}.png
}

